;무기
%100
+0
+42

;---------------------------------------------------------
;
;[104] 약사의편지를 주었는지
;[105] 허의원과 친구하기로 함
;[106] 식인초열매를 찾아 주기로 함
;[107] 식인초열매를 충분히 갖다 줌
;[108] 허의원의 첫번째 약을 받음
;
;----------------------------------------------------------

;대화, '*' : 대화 태그
[@main]
#IF
checkpkpoint 2
#SAY
我不想和你这样的人打交道.. \ \
<取消/@exit>

#ELSEACT
goto @main-1


[@main-1]
어서오시오.  무슨일로 오셨습니까?\ \
 약을 <산다/@buy>\
 약을 <판다/@sell>\
 <대화를 한다/@talkwith>\
 <그만둔다/@exit>
<>

[@buy]
어떤 물건이 필요하신가요?\ \ \
 <뒤로/@main>

[@sell]
팔 물건을 주시오. 가격은 잘 쳐드리죠.\ \ \
 <뒤로/@main>

;#####################################################

[@talkwith]
#IF
checkunit  [1] 0
#ACT
goto  @talkwith_1


;#####################################################


[@talkwith_1]
;편지에 대해 얘기함
#IF
check [103] 1
check [104] 0
check [105] 0
#ACT
goto @talk_about_letter
break

;친구가 되기로 하고 다시 왔음
#IF
check  [103]  1
check  [104]  1
check  [106]  0
#ACT
goto @talk_1_1
break

;식인초열매를 주러옴
#IF
check  [104]  1
check  [106]  1
check  [107]  0
#ACT
goto @talk_2_1
break

;허의원의약1을 얻으러 옴
#IF
check  [104]  1
check  [106]  1
check  [107]  1
check  [108]  0
#ACT
goto    @talk_3_1
break

;아무 조건도 없는 경우의 대화
#IF
check  [105] 1
#SAY
<$USERNAME> 미안하네.. 지금 바빠서 말야\
다음에 찾아 오면 안되겠는가...\
미안하네..\
#ACT
break

#IF
#SAY
미안하네.. 지금은 바쁘니 나중에 찾아오시게\


;-------------------------------------------------------
;편지를 전달하기 위해서 왔음
;-------------------------------------------------------

[@talk_about_letter]
... 당신이 아버님께서 보낸 편지를 가지고 있다는\
말이로군요. 무슨일로 편지를 보내셨을까?\
어서 편지를 주십시오.\ \
<편지를 전달한다./@give_letter>\
<잃어 버렸다고 한다./@no_give_letter>\

[@give_letter]
#IF
CHECKITEM  약사의편지  1
#ACT
TAKE  약사의편지  1
SET   [104]   1
#SAY
고맙소, 이렇게 수고스러운 일을 해주시다니\
정말 고맙구려, 이름이라도 알았으면 좋겠소\
나는 허의원이라고 하오\
당신의 이름은 무엇이오?\ \
<이름을 알려준다./@say_my_name>\
#ELSESAY
편지가 지금 없다니, 그럼 도중에 잃어 버렸다는\
말입니까? .... 찾는데로 갖다 주시오.\

[@no_give_letter]
아니, 편지를 잃어 버렸다니,...\
허허허,.. 됐소이다. 그렇다고 그렇게\
미안해 하니 내가 몸둘바를 모르겠소\
그만 됐으니 돌아 가시오\

[@say_my_name]
<$USERNAME> 이라, 만나서 반갑소\
이것도 우연인데 우리 친하게 지내봅시다.\
자 이거는 마음의 선물이니 받으시오\
선화수라는 물약인데 꽤 쓸만한 약이라오\ \
<선화수를 받는다./@ok_take_drug>

[@ok_take_drug]
#ACT
SET  [105]    1
GIVE 선화수(중) 1
#SAY
참, 그리고 다음에 꼭 다시 오시오...\
다음에 또 봅시다.\

;-------------------------------------------------------
; 편지를 전달하고 난 다음에 왔음. 식인초열매 주기 이전
;-------------------------------------------------------

[@talk_1_1]
#SAY
아 <$USERNAME>, 어서 오게나... \
자네는 잘 지내고 있겠지\
내가 지금 식인초열매가 필요한데 말야\
자네가 몇 개만 가져다 주면 내 높은 값을 쳐줌세\
꼭 그렇게 해주었으면 좋겠네만..\
기다리고 있겠네...\
#ACT
SET  [106] 1

;-------------------------------------------------------
; 식인초열매를 주러왔음
;-------------------------------------------------------

[@talk_2_1]
#IF
CHECKITEM  식인초열매  1
#SAY
역시 자네, 잊지 않고 왔구만\
식인초열매를 가져 왔으면 한개씩 주게나..\ \
<식인초 열매를 준다./@give_sg_1>\
#ELSEACT
GOTO  @talk_1_1

[@give_sg_1]
#IF
CHECKITEM  식인초열매 1
#ACT
TAKE 식인초열매 1
GIVE 돈 600
GOTO @check_enough_1

;식인초 열매를 충분히 받았는지 검사
[@check_enough_1]
#IF
RANDOM 10
DAYTIME SUNSET
#ACT
SET  [107] 1
#SAY
고맙네, <$USERNAME> 이제 식인초열매는\
충분하네, 다 자네 덕분이라네...\
내가 이제 약을 만들려고 하는데, 잘 되기를\
빌어 주게나...\
그럼 다음에 보세..\
#ELSEACT
GOTO @give_sg_2

[@give_sg_2]
#IF
CHECKITEM 식인초열매 1
#ACT
GOTO @talk_2_1
#ELSEACT
CLOSE


;-------------------------------------------------------
;허의원이 1번째 약을 만들고 있음
;-------------------------------------------------------

[@talk_3_1]
#if
daytime day
#act
goto    @talk_3_2
#elsesay
<$USERNAME>, 어서 오게나.. 아직 약이 완성되지\
않았다네.. 약이 완성되면 꼭 내 부탁을 들어주게\
빨리 약이 완성되야 할텐데...\

[@talk_3_2]
<$USERNAME>, 약을 만들긴 했네만,\
아직 해결하지 못한 부분이 있어서 스승님에게\
자문을 구해봐야 겠네. 자네에게 부탁이 있는데\
들어 줄 수 있는가?\ \
<부탁을 들어준다./@ok_h_order>\

[@ok_h_order]
고맙네, 이 약을 내 스승이신 유노의원에게\
가져다 주면서 이 약에서 빠진 부분이 무엇인지\
물어봐다 주게.. 나는 이 약방을 비울 수가 없으니\
자네가 좀 수고 해주게나..\
그런데 말야 스승님이 소식이 끊긴지가 오래되서\
이를 어쩐다... 친구 미안하지만 꼭 좀 부탁하네..\
<부탁을 들어준다./@ok_h_order_now>\

[@ok_h_order_now]
#act
set  [108] 1
give 허의원의약1 1
close

;-------------------------------------------------------
;
;-------------------------------------------------------

[goods]
;젠되는 상품    물량       hour
蜡烛                                  1000       1
随机传送卷                    1000       1
地牢逃脱卷                       1000       1
金创药(小量)                    1000        1
魔法药(小量)                    1000        1
金创药(中量)              1000        1
魔法药(中量)              1000        1
强效金创药              1000        1
强效魔法药              1000        1
金创药（小）包              1000        1
魔法药（小）包              1000        1
金创药（中）包              1000        1
魔法药（中）包               1000        1
超级金创药                    1000        1
超级魔法药                    1000        1
太阳水   	1000	1
强效太阳水	1000	1
万年雪霜		1000	1
















































